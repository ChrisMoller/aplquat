⍝ Quat.tc
⍝ tests from examples in Quaternions for Computer Graphics 2nd Edition
⍝ by John Vince
⍝ ═════

      ⎕pp←6
      
      'libaplquat.so' ⎕fx 'quat'
quat


⍝ creators

      quat 1 2 3 4
1 2 3 4
      quat 1      
1 0 0 0
      quat 1 2 3
0 1 2 3
      4 quat 1 2 3
4 1 2 3
      90 quat['deg'] 1 1 1
0.707107 0.707107 0.707107 0.707107
      0 quat['deg'] 1 1 1
1 0 0 0
      (○.5) quat['rad'] 1 1 1
0.707107 0.707107 0.707107 0.707107
      (○1) quat['rad'] 1 1 1
6.12323E¯17 1 1 1
      ((○1) quat['rad'] 1 1 1) quat['='] 180 quat['deg'] 1 1 1
1
      ((○1) quat['rad'] 1 1 1) quat['≠'] 180 quat['deg'] 1 1 1
0


      qa←quat 2 ¯2 3 ¯4
      qb←quat 1 ¯2 5 ¯6

⍝ addition/subtraction

      qa quat['+'] qb
3 ¯4 8 ¯10
      qa quat['-'] qb
1 0 ¯2 2

⍝ norm

      (quat['+'] qa)*2
33

⍝ unit norming

      qa quat['÷'] quat['+'] qa
0.348155 ¯0.348155 0.522233 ¯0.696311
     quat ['+'] qa quat['÷'] quat['+'] qa
1

⍝ multiplication/division

⍝ quaternion multiplication is not commutative
     qa quat['×'] qb
¯41 ¯4 9 ¯20
      qb quat['×'] qa
¯41 ¯8 17 ¯12
      qa quat['×'] qa
¯25 ¯8 12 ¯16
      qa quat['×'] 5
10 ¯10 15 ¯20
      5 quat['×'] qa
10 ¯10 15 ¯20
      (qb quat['÷'] qa) quat['×'] qa
1 ¯2 5 ¯6


⍝ inverse

      33 quat['×'] quat['÷'] qa
2 2 ¯3 4
      qa quat['×'] quat['÷'] qa
1 0 0 0

⍝ complex conjugate

      quat['*'] quat 2 3 ¯4 5
2 ¯3 4 ¯5

⍝ dots and crosses

      (quat 0 0 0 1) quat['dot'] quat 0 0 0 1
1
      (quat 0 0 0 1) quat['dot'] quat 0 0 1 0
0
      (quat 0 0 0 1) quat['cross'] quat 0 0 0 1
0 0 0

⍝   i cross j  = k
      (quat 0 1 0 0) quat['cross'] quat 0 0 1 0
0 0 1

⍝   j cross k = i
      (quat 0 0 1 0) quat['cross'] quat 0 0 0 1
1 0 0

⍝   k cross i = j
      (quat 0 0 0 1) quat['cross'] quat 0 1 0 0
0 1 0

⍝ rotation

      p←quat 0 0 1 0		⍝ point at 1.0 on y axis
      theta←90     		⍝ rotation angle 90 degrees
      q←theta quat['deg'] 1 0 0 	   ⍝ around the x axis
      q quat['×'] p quat['×'] quat['÷'] q
0 0 2.22045E¯16 1
      q quat['×'] p quat['÷'] q
0 0 2.22045E¯16 1
      

      theta←360
      q←theta quat['deg'] 1 0 0
      q quat['×'] p quat['×'] quat['÷'] q
0 0 1 ¯2.44929E¯16
      q quat['×'] p quat['÷'] q
0 0 1 ¯2.44929E¯16

⍝ spin
      p←quat 0 0 1 0
      r45 ← 45 quat['deg'] 1 0 0
      q←r45
      
      q quat['×'] p quat['÷'] q		
0 0 0.707107 0.707107

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 ¯2.22045E¯16 1	

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 ¯0.707107 0.707107

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 ¯1 ¯2.22045E¯16

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 ¯0.707107 ¯0.707107

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 3.33067E¯16 ¯1

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 0.707107 ¯0.707107

      q←r45 quat['×'] q
      q quat['×'] p quat['÷'] q		
0 0 1 3.33067E¯16

